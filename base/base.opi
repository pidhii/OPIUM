load "base.so"

let (+) x y = x + y
let (-) x y = x - y
let (*) x y = x * y
let (/) x y = x / y
let (mod) x y = x mod y
let (fmod) x y = x fmod y
let (:) x y = x : y
let (is not) x y = x is not y
let (not eq) x y = x not eq y
let (not equal) x y = x not equal y
let (||) x y = x || y
let (&&) x y = x && y
let (==) x y = x == y
let (/=) x y = x /= y
let (<) x y = x < y
let (>) x y = x > y
let (<=) x y = x <= y
let (>=) x y = x >= y

let const x = \_ -> x
let flip f = \x y -> f y x
let uncurry f (p1:p2) = f p1 p2

let zero? x = x == 0
let positive? x = x > 0
let negative? x = x < 0
let even? x = x fmod 2 == 0
let odd? x = x fmod 2 /= 0

let trim = ltrim . rtrim

let reverse l = revappend l nil

--------------------------------------------------------------------------------
-- Seq utilities
--
trait ToSeq =
  let toSeq
end

trait ToList =
  let toList
end

trait ToRevSeq =
  let toRevSeq
end

impl ToSeq for Seq   = let toSeq = id                 end
impl ToSeq for Cons  = let toSeq = List.toSeq         end
impl ToSeq for Nil   = let toSeq = List.toSeq         end
impl ToSeq for Table = let toSeq = List.toSeq . pairs end
impl ToSeq for Array = let toSeq = Array.toSeq        end

impl ToList for Array = let toList = Array.toList end
impl ToList for Table = let toList = pairs        end

-- TODO: Array.toSeq, Array.foldl, Array.foldr, List.foldl
let rlist x =
  if x is nil then x
  else if x is Cons then reverse x
  else if x is Array then Array.toRevList x
  else List.ofRevSeq $ ToSeq.toSeq x

let list x = --reverse . rlist
  if x is Cons || x is nil then x
  else if x is ToList then ToList.toList x
  else if x is ToRevSeq then rlist $ ToRevSeq.toRevSeq x
  else reverse $ rlist x

let array x =
  if x is Array then x
  else Array.ofSeq $ ToSeq.toSeq x

let table x =
  if x is Table then x
  else Table $ list x

let foreach f = Seq.iter f . ToSeq.toSeq
let map f = Seq.map f . ToSeq.toSeq
let zip x y = Seq.zip (ToSeq.toSeq x) (ToSeq.toSeq y)
let filter f = Seq.filter f . ToSeq.toSeq
let foldl f z = Seq.foldl f z . ToSeq.toSeq

let unfold = Seq.unfold

let range from to =
  unfold (fn i -> if i < to then i : i + 1) from

--let gt x y = x > y
let take n = map cdr . zip (range 0 n)
let drop n = map cdr . filter (fn (x:_) -> x >= n) . zip (range 0 inf)

let rec any? f l =
  unless let x:xs = l then false
  else f x || any? f xs
let any? f = any? f . list

let rec all? f l =
  unless let x:xs = l then true
  else f x && all? f xs
let all? f = all? f . list

let join sep l =
  let start = [nil] in
  foldl (\acc x -> if acc is start then x else acc ++ sep ++ x) start l

--------------------------------------------------------------------------------
-- I/O
--
module File =
  let dup file ..mode = __base_file_dup file (car mode or "r")
end

let open path ..mode = __base_open path (car mode or "r")
let popen cmd ..mode = __base_popen cmd (car mode or "r")

let write x = printf "%w" x
let display x = printf "%d" x

let read = __base_read
let readline ..x = __base_readline (car x or stdin)
let readlines file =
  assert file is File;
  unfold (fn s ->
    let file =
      if let [file] = s then
        File.dup file -- duplicate file stream to own cursor
      else s
    in
    let line = readline file in
    if line then line : file else nil
  ) [file] -- to indicate first iteration

impl ToSeq for File =
  let toSeq = readlines
end

